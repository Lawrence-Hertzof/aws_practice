
resource "aws_acm_certificate" "this" {
  domain_name       = "learning-aws-terraform.com"
  validation_method = "DNS"  

  subject_alternative_names = [
    "www.learning-aws-terraform.com",
  ]

  lifecycle {
    create_before_destroy = true
  }
}

### Validation Record ###

resource "aws_route53_record" "cert_validation" {
  for_each = {
    for dvo in aws_acm_certificate.this.domain_validation_options : dvo.domain_name => {
      name   = dvo.resource_record_name
      record = dvo.resource_record_value
      type   = dvo.resource_record_type
    }
  }

  zone_id = var.route53_zone_id
  name    = each.value.name
  type    = each.value.type
  ttl     = 60
  records = [each.value.record]
}

resource "aws_acm_certificate_validation" "this" {
  certificate_arn         = aws_acm_certificate.this.arn
  validation_record_fqdns = [for record in aws_route53_record.cert_validation : record.fqdn]
}

